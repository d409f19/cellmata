plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'antlr'
}

group 'dk.aau.cs.d409f19'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

task run(type: JavaExec) {
    main = 'dk.aau.cs.d409f19.cellumata.MainKt'
    classpath = sourceSets.main.runtimeClasspath

    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'dk.aau.cs.d409f19.cellumata.MainKt'
    }

    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself resulting in a 'fat jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    antlr "org.antlr:antlr4:4.7.2"
    testImplementation("org.junit.jupiter:junit-jupiter:5.4.1")
}

compileKotlin {
    dependsOn generateGrammarSource
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    dependsOn generateGrammarSource
    kotlinOptions.jvmTarget = "1.8"
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages", "-package", "dk.aau.cs.d409f19.antlr"]
    outputDirectory = file("${project.buildDir}/generated-src/antlr/main/dk/aau/cs/d409f19/antlr")
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

sourceSets {
    main.java.srcDirs += "${project.buildDir}/generated-src/antlr/main"
}
